buildscript {
	ext {
		springBootVersion = '1.5.8.RELEASE'
	}
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
	}
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'

group = 'com.example'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8

repositories {
	flatDir {
       dirs "APP_HOME/config/library"
   	}
	mavenCentral()
}

dependencies {
	
	// spring
	
	compile('org.springframework.boot:spring-boot-starter-actuator')
	
	compile('org.springframework.boot:spring-boot-actuator-docs')
	
	compile('org.springframework.boot:spring-boot-starter-jdbc')
	
	compile('org.mybatis.spring.boot:mybatis-spring-boot-starter:1.3.1')
	
	compile('org.springframework.boot:spring-boot-starter-thymeleaf')
	
	compile('org.springframework.boot:spring-boot-starter-web')
	
	compile('org.springframework.boot:spring-boot-starter-batch')
	
	compile('org.springframework.boot:spring-boot-starter-mail')
	
	compile('org.springframework.boot:spring-boot-configuration-processor')
	
	runtime('org.springframework.boot:spring-boot-devtools')
	
	// other

	compile('org.liquibase:liquibase-core')
	
	compile('commons-lang:commons-lang:2.6')
	
	compile('commons-beanutils:commons-beanutils:1.8.3')
	
	compile('org.apache.httpcomponents:httpclient:4.5.2')
	
	compile('commons-fileupload:commons-fileupload:1.3.3')
	
	compile('org.quartz-scheduler:quartz:2.3.0')
	
	compile('mysql:mysql-connector-java:5.1.21')
	
	runtime('com.h2database:h2')
	
	compileOnly('org.projectlombok:lombok')
	
	// test
	
	testCompile('org.springframework.boot:spring-boot-starter-test')
}

test.onlyIf { false }

springBoot {
    executable = true
    embeddedLaunchScriptProperties = [
    	initInfoProvides: project.name,
        initInfoShortDescription: 'Spring Boot Sample Application',
    	initInfoDescription: 'Spring Boot Sample Application',
        mode: 'service'
  	]
}

def deployDir = "${buildDir}/deploy"
def jarFileName = "${project.name}-${project.version}"

task exportDeploy(dependsOn: ['exportAppHome', 'exportJar', 'exportConfigFile']) {}

task exportAppHome {
	doLast {
		copy {
			from "APP_HOME/"
			into "${deployDir}/APP_HOME/"
			exclude '**/log/'
		}
	}
}

task exportJar(dependsOn: build) {
	doLast {
		copy {
			from "${buildDir}/libs/${jarFileName}.jar"
			into "${deployDir}"
		}
	}
}

task exportConfigFile {
	doLast {
		def confFile = new File("${deployDir}/${jarFileName}.conf")
		def content = "JAVA_OPTS=" + "\"-DAPP_HOME=${deployDir}/APP_HOME -Dspring.config.location=${deployDir}/APP_HOME/${appName}/config/\""
		confFile.write content
	}
}

build.finalizedBy(exportDeploy)

